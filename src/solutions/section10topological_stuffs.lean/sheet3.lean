/-
Copyright (c) 2023 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author : Jujian Zhang
-/

import ring_theory.ideal.operations
import topology.algebra.polynomial
import topology.bases

/-!
# Prime spectrum of a ring and its Zariski topology

This files contains the following: 
- Zariski topology on the set of all prime ideals of any ring `R`.
- a basis for Zariski topology 
- if `f : R ‚Üí+* S` is a ring homomorphism, then `ùî≠ ‚Ü¶ f‚Åª¬π ùî≠` is continuous. 
- for integral domains, there is a unique generic point.
-/

open topological_space
open_locale pointwise

universe u

variables (R S : Type u) [comm_ring R] [comm_ring S]

/--
`Spec R` is the set of prime ideals of `R`
-/
@[ext]
structure Spec : Type u := 
(as_ideal : ideal R)
(is_prime : as_ideal.is_prime)

attribute [instance] Spec.is_prime -- making sure class inference knows term of `Spec R` is prime

section

variable {R}

/--
zero locus of a set `s ‚äÜ R` is the set of all prime ideals larger than `s`.

if `f : R`, then it defines a function `ùî≠ ‚Ü¶ ([f] : R ‚ß∏ ùî≠)`.

So `V s` is exactly those primes
vanishing for all `f ‚àà s`.
-/
def V (s : set R) : set (Spec R) :=
{ I : Spec R | s ‚äÜ I.as_ideal }

lemma mem_V (s : set R) {p : Spec R} : p ‚àà V s ‚Üî s ‚äÜ p.as_ideal := 
iff.rfl

/--
empty set is zero locus of `R`
-/
lemma V_univ : V (set.univ : set R) = ‚àÖ :=
begin 
  rw set.eq_empty_iff_forall_not_mem,
  intros p,
  rw [mem_V, set.univ_subset_iff],
  have mem1 : (1 : R) ‚àâ (p.as_ideal : set R) := p.as_ideal.ne_top_iff_one.mp p.is_prime.ne_top,
  intros r,
  rw r at mem1,
  exact mem1 ‚ü®‚ü©,
end

/--
R is zero locus of `‚àÖ`
-/
lemma V_empty : V (‚àÖ : set R) = set.univ :=
set.eq_univ_iff_forall.mpr $ Œª p x r, false.elim $ (set.mem_empty_iff_false _).mp r

/--
union of zero loci is zero locus of pointwise product
-/
lemma V_union (s t : set R) : V s ‚à™ V t = V (s * t) :=
begin 
  ext p,
  split,
  { rintros (hp|hp);
    rw mem_V at hp ‚ä¢;
    intros r hr;
    obtain ‚ü®a, b, ha, hb, rfl‚ü© := hr,
    { specialize hp ha,
      exact p.as_ideal.mul_mem_right _ hp, },
    { specialize hp hb,
      exact p.as_ideal.mul_mem_left _ hp, }, },
  { intros hp,
    rw mem_V at hp,
    rw [set.mem_union, mem_V, mem_V],
    contrapose! hp,
    simp only [set.not_subset_iff_exists_mem_not_mem] at hp ‚ä¢,
    obtain ‚ü®‚ü®a, ha1, ha2‚ü©, ‚ü®b, hb1, hb2‚ü©‚ü© := hp,
    exact ‚ü®_, ‚ü®a, b, ha1, hb1, rfl‚ü©, Œª r, (p.is_prime.mem_or_mem r).elim 
      (Œª h, ha2 h) (Œª h, hb2 h)‚ü©, },
end

/--
intersection of zero loci is zero locus of union
-/
lemma V_inter {Œπ : Sort*} (s : Œπ ‚Üí set R) : 
  (‚ãÇ i : Œπ, V (s i)) = V (‚ãÉ i, (s i)) :=
begin 
  ext p, 
  split;
  intros hp,
  { simp_rw [set.mem_Inter, mem_V] at hp,
    rw [mem_V, set.Union_subset_iff],
    assumption, },
  { rw [mem_V, set.Union_subset_iff] at hp,
    simp_rw [set.mem_Inter, mem_V],
    assumption, },
end

end

instance Zariski_topology : topological_space (Spec R) :=
topological_space.of_closed 
{ t | ‚àÉ (s : set R), t = V s } ‚ü®set.univ, V_univ.symm‚ü© 
begin 
  rintros _ hS,
  choose a ha using hS,
  rw [set.sInter_eq_bInter],
  suffices : (‚ãÇ (i : set (Spec R)) (h : i ‚àà A), i) = ‚ãÇ (i : A), V (a i.2),
  { rw [this, V_inter],
    exact ‚ü®_, rfl‚ü©, },
  simp only [subtype.val_eq_coe, subtype.coe_mk, set.Inter_coe_set],
  exact set.Inter_congr (Œª s, set.Inter_congr $ Œª hs, ha hs),
end
begin 
  rintros _ ‚ü®s, rfl‚ü© _ ‚ü®t, rfl‚ü©,
  rw V_union,
  exact ‚ü®s * t, rfl‚ü©,
end

/--
open sets of Zariski topology are complement of zero loci
-/
lemma zt_is_open (s : set (Spec R)) : is_open s ‚Üî ‚àÉ (t : set R), s = (V t)·∂ú :=
begin 
  change (‚àÉ _, _) ‚Üî _,
  rw [exists_congr],
  exact Œª a, by rw [eq_compl_comm, eq_comm],
end

section

variables {R S}

/--
Basic open sets
-/
def D (f : R) : set (Spec R) := (V {f})·∂ú

lemma mem_D (f : R) (p : Spec R) : p ‚àà D f ‚Üî f ‚àâ p.as_ideal :=
begin 
  split;
  intros hp hf;
  rw [D, set.mem_compl_iff, mem_V, set.singleton_subset_iff] at hp <|>
  rw [mem_V, set.singleton_subset_iff] at hf;
  exact hp hf,
end

lemma open_D (f : R) : is_open (D f) :=
begin 
  rw [D, zt_is_open],
  exact ‚ü®_, rfl‚ü©,
end

/--
Basic open sets form a basis
-/
lemma D_is_basis : is_topological_basis (set.range (D : R ‚Üí set (Spec R))) :=
is_topological_basis_of_open_of_nhds (by { rintros _ ‚ü®r, -, rfl‚ü©, exact open_D _ }) $ 
Œª p s hp hs, begin
  simp only [set.mem_range, exists_prop, exists_exists_eq_and, mem_D],
  rw zt_is_open at hs,
  obtain ‚ü®s, rfl‚ü© := hs,
  rw [set.mem_compl_iff, mem_V, set.not_subset_iff_exists_mem_not_mem] at hp,
  obtain ‚ü®x, hx1, hx2‚ü© := hp,
  refine ‚ü®x, hx2, Œª y hy H, _‚ü©,
  exact (mem_D _ _).mp hy (H hx1),
end

/--
Ring homomorphisms induces continuous map (contravariantly).
-/
def comap (f : R ‚Üí+* S) : Spec S ‚Üí Spec R :=
Œª p, ‚ü®p.as_ideal.comap f, infer_instance‚ü©

lemma comap_as_ideal (f : R ‚Üí+* S) (p : Spec S) : (comap f p).as_ideal = p.as_ideal.comap f := rfl

lemma continuous_comap (f : R ‚Üí+* S) : continuous (comap f) :=
begin 
  refine D_is_basis.continuous _ _,
  rintros _ ‚ü®r, rfl‚ü©,
  rw show comap f ‚Åª¬π' D r  = D (f r),
  { ext1 p, 
    simp only [set.mem_preimage, mem_D, comap_as_ideal, ideal.mem_comap], },
  exact open_D _,
end

local notation `‚Ñ§[X]` := (polynomial ‚Ñ§)
-- every thing from this points work for a generic integral domain
/--
the point corresponding to the zero ideal.
-/
@[simps]
noncomputable def Œ∑ : Spec ‚Ñ§[X] :=
{ as_ideal := ‚ä•,
  is_prime := 
  { ne_top' := begin 
      rw [ideal.ne_top_iff_one, ideal.mem_bot],
      norm_num,
    end,
    mem_or_mem' := Œª x y hxy, 
    begin 
      simp only [ideal.mem_bot] at hxy ‚ä¢,
      rwa mul_eq_zero at hxy,
    end } }

/--
this is a generic point.
-/
lemma generic_Œ∑ : closure {Œ∑} = (set.univ : set (Spec ‚Ñ§[X])) :=
begin 
  rw show closure {Œ∑} = V (Œ∑.as_ideal : set ‚Ñ§[X]),
  { ext,
    rw [mem_V, mem_closure_iff, Œ∑_as_ideal],
    split,
    { intros h r hr,
      rw [set_like.mem_coe, ideal.mem_bot] at hr,
      rw hr,
      exact ideal.zero_mem _, },
    { rintros - o ho hx,
      rw zt_is_open at ho,
      obtain ‚ü®o, rfl‚ü© := ho,
      rw [set.mem_compl_iff, mem_V, set.not_subset_iff_exists_mem_not_mem] at hx,
      obtain ‚ü®q, hq1, hq2‚ü© := hx,
      by_cases q0 : q = 0,
      { rw [q0] at *,
        exfalso,
        refine hq2 (ideal.zero_mem _), },
      rw show (V o)·∂ú ‚à© {Œ∑} = {Œ∑},
      { rw [set.inter_eq_right_iff_subset, set.singleton_subset_iff,
          set.mem_compl_iff, mem_V, set.not_subset_iff_exists_mem_not_mem, Œ∑_as_ideal],
        exact ‚ü®q, hq1, q0‚ü©, },
      exact ‚ü®Œ∑, set.mem_singleton _‚ü©, }, },
  rw [Œ∑_as_ideal, set.eq_univ_iff_forall],
  intros x,
  rw [mem_V],
  intros y hy,
  rw [set_like.mem_coe, ideal.mem_bot] at hy,
  rw hy,
  exact ideal.zero_mem _,
end

/--
Generic points is unique.
-/
lemma generic_point_uniq (x : Spec ‚Ñ§[X]) (hx : closure {x} = (set.univ : set (Spec ‚Ñ§[X]))) :
  x = Œ∑ :=
begin 
  have h : closure {x} = closure {Œ∑},
  { rw [generic_Œ∑, hx], },
  have H : ‚àÄ (a b : Spec ‚Ñ§[X]), a.as_ideal ‚â§ b.as_ideal ‚Üî b ‚àà closure {a},
  { intros a b,
    split,
    { rw mem_closure_iff,
      intros hle o ho hb,
      rw zt_is_open at ho,
      obtain ‚ü®o, rfl‚ü© := ho,
      rw [set.mem_compl_iff, mem_V, set.not_subset_iff_exists_mem_not_mem] at hb,
      obtain ‚ü®q, hq1, hq2‚ü© := hb,
      rw [set.inter_nonempty], 
      simp_rw [set.mem_compl_iff, mem_V, set.not_subset_iff_exists_mem_not_mem, 
        set_like.mem_coe],
      exact ‚ü®a, ‚ü®q, hq1, Œª h, hq2 (hle h)‚ü©, set.mem_singleton _‚ü©, },
    { intros hmem p hp,
      rw [mem_closure_iff] at hmem,
      contrapose! hp,
      specialize hmem (D p) (open_D p) ((mem_D _ _).mpr hp),
      obtain ‚ü®x, hx1, hx2‚ü© := hmem,
      rw set.mem_singleton_iff at hx2,
      rw hx2 at *,
      rwa mem_D at hx1, }, },
  have Hle1 : x.as_ideal ‚â§ Œ∑.as_ideal,
  { rw [H, h],
    exact subset_closure (set.mem_singleton _), },
  have Hle2 : Œ∑.as_ideal ‚â§ x.as_ideal,
  { rw [H, ‚Üêh],
    exact subset_closure (set.mem_singleton _), },
  ext1,
  exact le_antisymm Hle1 Hle2,
end

end
